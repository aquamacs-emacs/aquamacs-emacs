<html lang="en">
<head>
<title>Programming Example - The Emacs Widget Library</title>
<meta http-equiv="Content-Type" content="text/html">
<meta name="description" content="The Emacs Widget Library">
<meta name="generator" content="makeinfo 4.13">
<link title="Top" rel="start" href="index.html#Top">
<link rel="prev" href="User-Interface.html#User-Interface" title="User Interface">
<link rel="next" href="Setting-Up-the-Buffer.html#Setting-Up-the-Buffer" title="Setting Up the Buffer">
<link href="http://www.gnu.org/software/texinfo/" rel="generator-home" title="Texinfo Homepage">
<!--
Copyright (C) 2000, 2001, 2002, 2003, 2004, 2005, 2006, 2007,
2008, 2009, 2010  Free Software Foundation, Inc.

     Permission is granted to copy, distribute and/or modify this
     document under the terms of the GNU Free Documentation License,
     Version 1.3 or any later version published by the Free Software
     Foundation; with no Invariant Sections, with the Front-Cover texts
     being ``A GNU Manual'', and with the Back-Cover Texts as in (a)
     below.  A copy of the license is included in the section entitled
     ``GNU Free Documentation License''.

     (a) The FSF's Back-Cover Text is: ``You have the freedom to copy
     and modify this GNU manual.  Buying copies from the FSF supports
     it in developing GNU and promoting software freedom.''
   -->
<meta http-equiv="Content-Style-Type" content="text/css">
<style type="text/css"><!--
  pre.display { font-family:inherit }
  pre.format  { font-family:inherit }
  pre.smalldisplay { font-family:inherit; font-size:smaller }
  pre.smallformat  { font-family:inherit; font-size:smaller }
  pre.smallexample { font-size:smaller }
  pre.smalllisp    { font-size:smaller }
  span.sc    { font-variant:small-caps }
  span.roman { font-family:serif; font-weight:normal; } 
  span.sansserif { font-family:sans-serif; font-weight:normal; } 
--></style>
</head>
<body>
<div class="node">
<a name="Programming-Example"></a>
<p>
Next:&nbsp;<a rel="next" accesskey="n" href="Setting-Up-the-Buffer.html#Setting-Up-the-Buffer">Setting Up the Buffer</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="User-Interface.html#User-Interface">User Interface</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="index.html#Top">Top</a>
<hr>
</div>

<!-- node-name,  next,  previous,  up -->
<h3 class="section">Programming Example</h3>

<p><a name="index-widgets_002c-programming-example-16"></a><a name="index-example-of-using-widgets-17"></a>Here is the code to implement the user interface example (see <a href="User-Interface.html#User-Interface">User Interface</a>).

<pre class="lisp">     (require 'widget)
     
     (eval-when-compile
       (require 'wid-edit))
     
     (defvar widget-example-repeat)
     
     (defun widget-example ()
       "Create the widgets from the Widget manual."
       (interactive)
       (switch-to-buffer "*Widget Example*")
       (kill-all-local-variables)
       (make-local-variable 'widget-example-repeat)
       (let ((inhibit-read-only t))
         (erase-buffer))
       (remove-overlays)
       (widget-insert "Here is some documentation.\n\n")
       (widget-create 'editable-field
                      :size 13
                      :format "Name: %v " ; Text after the field!
                      "My Name")
       (widget-create 'menu-choice
                      :tag "Choose"
                      :value "This"
                      :help-echo "Choose me, please!"
                      :notify (lambda (widget &amp;rest ignore)
                                (message "%s is a good choice!"
                                         (widget-value widget)))
                      '(item :tag "This option" :value "This")
                      '(choice-item "That option")
                      '(editable-field :menu-tag "No option" "Thus option"))
       (widget-create 'editable-field
                      :format "Address: %v"
                      "Some Place\nIn some City\nSome country.")
       (widget-insert "\nSee also ")
       (widget-create 'link
                      :notify (lambda (&amp;rest ignore)
                                (widget-value-set widget-example-repeat
                                                  '("En" "To" "Tre"))
                                (widget-setup))
                      "other work")
       (widget-insert
         " for more information.\n\nNumbers: count to three below\n")
       (setq widget-example-repeat
             (widget-create 'editable-list
                            :entry-format "%i %d %v"
                            :notify (lambda (widget &amp;rest ignore)
                                      (let ((old (widget-get widget
                                                             ':example-length))
                                            (new (length (widget-value widget))))
                                        (unless (eq old new)
                                          (widget-put widget ':example-length new)
                                          (message "You can count to %d." new))))
                            :value '("One" "Eh, two?" "Five!")
                            '(editable-field :value "three")))
       (widget-insert "\n\nSelect multiple:\n\n")
       (widget-create 'checkbox t)
       (widget-insert " This\n")
       (widget-create 'checkbox nil)
       (widget-insert " That\n")
       (widget-create 'checkbox
                      :notify (lambda (&amp;rest ignore) (message "Tickle"))
                      t)
       (widget-insert " Thus\n\nSelect one:\n\n")
       (widget-create 'radio-button-choice
                      :value "One"
                      :notify (lambda (widget &amp;rest ignore)
                                (message "You selected %s"
                                         (widget-value widget)))
                      '(item "One") '(item "Another One.") '(item "A Final One."))
       (widget-insert "\n")
       (widget-create 'push-button
                      :notify (lambda (&amp;rest ignore)
                                (if (= (length (widget-value widget-example-repeat))
                                       3)
                                    (message "Congratulation!")
                                  (error "Three was the count!")))
                      "Apply Form")
       (widget-insert " ")
       (widget-create 'push-button
                      :notify (lambda (&amp;rest ignore)
                                (widget-example))
                      "Reset Form")
       (widget-insert "\n")
       (use-local-map widget-keymap)
       (widget-setup))
</pre>
   </body></html>

