<html lang="en">
<head>
<title>Example Major Modes - GNU Emacs Lisp Reference Manual</title>
<meta http-equiv="Content-Type" content="text/html; charset=utf-8">
<meta name="description" content="GNU Emacs Lisp Reference Manual">
<meta name="generator" content="makeinfo 4.13">
<link title="Top" rel="start" href="index.html#Top">
<link rel="up" href="Major-Modes.html#Major-Modes" title="Major Modes">
<link rel="prev" href="Generic-Modes.html#Generic-Modes" title="Generic Modes">
<link href="http://www.gnu.org/software/texinfo/" rel="generator-home" title="Texinfo Homepage">
<!--
This is the `GNU Emacs Lisp Reference Manual'
corresponding to Emacs version 24.3.50.

Copyright (C) 1990--1996, 1998--2013 Free Software Foundation, Inc.

     Permission is granted to copy, distribute and/or modify this
     document under the terms of the GNU Free Documentation License,
     Version 1.3 or any later version published by the Free Software
     Foundation; with the Invariant Sections being ``GNU General Public
     License,'' with the Front-Cover texts being ``A GNU Manual,'' and
     with the Back-Cover Texts as in (a) below.  A copy of the license
     is included in the section entitled ``GNU Free Documentation
     License.''

     (a) The FSF's Back-Cover Text is: ``You have the freedom to copy
     and modify this GNU manual.  Buying copies from the FSF supports
     it in developing GNU and promoting software freedom.''
   -->
<meta http-equiv="Content-Style-Type" content="text/css">

<link rel="stylesheet" href="aquamacs.css" type="text/css" /></head>
<body>
<div class="node">
<a name="Example-Major-Modes"></a>
<p>
Previous:&nbsp;<a rel="previous" accesskey="p" href="Generic-Modes.html#Generic-Modes">Generic Modes</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="Major-Modes.html#Major-Modes">Major Modes</a>
<hr>
</div>

<h4 class="subsection">23.2.9 Major Mode Examples</h4>

<p>Text mode is perhaps the simplest mode besides Fundamental mode. 
Here are excerpts from  <samp><span class="file">text-mode.el</span></samp> that illustrate many of
the conventions listed above:

<pre class="smallexample">     ;; <span class="roman">Create the syntax table for this mode.</span>
     (defvar text-mode-syntax-table
       (let ((st (make-syntax-table)))
         (modify-syntax-entry ?\" ".   " st)
         (modify-syntax-entry ?\\ ".   " st)
         ;; Add `p' so M-c on `hello' leads to `Hello', not `hello'.
         (modify-syntax-entry ?' "w p" st)
         st)
       "Syntax table used while in `text-mode'.")
     
     ;; <span class="roman">Create the keymap for this mode.</span>
     (defvar text-mode-map
       (let ((map (make-sparse-keymap)))
         (define-key map "\e\t" 'ispell-complete-word)
         map)
       "Keymap for `text-mode'.
     Many other modes, such as `mail-mode', `outline-mode' and
     `indented-text-mode', inherit all the commands defined in this map.")
</pre>
   <p>Here is how the actual mode command is defined now:

<pre class="smallexample">     (define-derived-mode text-mode nil "Text"
       "Major mode for editing text written for humans to read.
     In this mode, paragraphs are delimited only by blank or white lines.
     You can thus get the full benefit of adaptive filling
      (see the variable `adaptive-fill-mode').
     \\{text-mode-map}
     Turning on Text mode runs the normal hook `text-mode-hook'."
       (set (make-local-variable 'text-mode-variant) t)
       (set (make-local-variable 'require-final-newline)
            mode-require-final-newline)
       (set (make-local-variable 'indent-line-function) 'indent-relative))
</pre>
   <p class="noindent">(The last line is redundant nowadays, since <code>indent-relative</code> is
the default value, and we'll delete it in a future version.)

   <p><a name="index-g_t_0040file_007blisp_002dmode_002eel_007d-1738"></a>  The three Lisp modes (Lisp mode, Emacs Lisp mode, and Lisp Interaction
mode) have more features than Text mode and the code is correspondingly
more complicated.  Here are excerpts from <samp><span class="file">lisp-mode.el</span></samp> that
illustrate how these modes are written.

   <p>Here is how the Lisp mode syntax and abbrev tables are defined:

   <p><a name="index-syntax-table-example-1739"></a>
<pre class="smallexample">     ;; <span class="roman">Create mode-specific table variables.</span>
     (defvar lisp-mode-abbrev-table nil)
     (define-abbrev-table 'lisp-mode-abbrev-table ())
     
     (defvar lisp-mode-syntax-table
       (let ((table (copy-syntax-table emacs-lisp-mode-syntax-table)))
         (modify-syntax-entry ?\[ "_   " table)
         (modify-syntax-entry ?\] "_   " table)
         (modify-syntax-entry ?# "' 14" table)
         (modify-syntax-entry ?| "\" 23bn" table)
         table)
       "Syntax table used in `lisp-mode'.")
</pre>
   <p>The three modes for Lisp share much of their code.  For instance,
each calls the following function to set various variables:

<pre class="smallexample">     (defun lisp-mode-variables (&amp;optional syntax keywords-case-insensitive)
       (when syntax
         (set-syntax-table lisp-mode-syntax-table))
       (setq local-abbrev-table lisp-mode-abbrev-table)
       ...
</pre>
   <p class="noindent">Amongst other things, this function sets up the <code>comment-start</code>
variable to handle Lisp comments:

<pre class="smallexample">       (make-local-variable 'comment-start)
       (setq comment-start ";")
       ...
</pre>
   <p>Each of the different Lisp modes has a slightly different keymap.  For
example, Lisp mode binds <kbd>C-c C-z</kbd> to <code>run-lisp</code>, but the other
Lisp modes do not.  However, all Lisp modes have some commands in
common.  The following code sets up the common commands:

<pre class="smallexample">     (defvar lisp-mode-shared-map
       (let ((map (make-sparse-keymap)))
         (define-key map "\e\C-q" 'indent-sexp)
         (define-key map "\177" 'backward-delete-char-untabify)
         map)
       "Keymap for commands shared by all sorts of Lisp modes.")
</pre>
   <p class="noindent">And here is the code to set up the keymap for Lisp mode:

<pre class="smallexample">     (defvar lisp-mode-map
       (let ((map (make-sparse-keymap))
     	(menu-map (make-sparse-keymap "Lisp")))
         (set-keymap-parent map lisp-mode-shared-map)
         (define-key map "\e\C-x" 'lisp-eval-defun)
         (define-key map "\C-c\C-z" 'run-lisp)
         ...
         map)
       "Keymap for ordinary Lisp mode.
     All commands in `lisp-mode-shared-map' are inherited by this map.")
</pre>
   <p class="noindent">Finally, here is the major mode command for Lisp mode:

<pre class="smallexample">     (define-derived-mode lisp-mode prog-mode "Lisp"
       "Major mode for editing Lisp code for Lisps other than GNU Emacs Lisp.
     Commands:
     Delete converts tabs to spaces as it moves back.
     Blank lines separate paragraphs.  Semicolons start comments.
     
     \\{lisp-mode-map}
     Note that `run-lisp' may be used either to start an inferior Lisp job
     or to switch back to an existing one.
     
     Entry to this mode calls the value of `lisp-mode-hook'
     if that value is non-nil."
       (lisp-mode-variables nil t)
       (set (make-local-variable 'find-tag-default-function)
            'lisp-find-tag-default)
       (set (make-local-variable 'comment-start-skip)
            "\\(\\(^\\|[^\\\\\n]\\)\\(\\\\\\\\\\)*\\)\\(;+\\|#|\\) *")
       (setq imenu-case-fold-search t))
</pre>
   </body></html>

