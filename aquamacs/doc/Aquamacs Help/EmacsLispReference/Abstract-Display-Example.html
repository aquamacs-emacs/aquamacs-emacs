<!DOCTYPE html PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN" "http://www.w3.org/TR/html4/loose.dtd">
<html>
<!-- This is the GNU Emacs Lisp Reference Manual
corresponding to Emacs version 25.1.

Copyright Â© 1990-1996, 1998-2016 Free Software Foundation, Inc.

Permission is granted to copy, distribute and/or modify this document
under the terms of the GNU Free Documentation License, Version 1.3 or
any later version published by the Free Software Foundation; with the
Invariant Sections being "GNU General Public License," with the
Front-Cover Texts being "A GNU Manual," and with the Back-Cover
Texts as in (a) below.  A copy of the license is included in the
section entitled "GNU Free Documentation License."

(a) The FSF's Back-Cover Text is: "You have the freedom to copy and
modify this GNU manual.  Buying copies from the FSF supports it in
developing GNU and promoting software freedom." -->
<!-- Created by GNU Texinfo 5.2, http://www.gnu.org/software/texinfo/ -->
<head>
<title>GNU Emacs Lisp Reference Manual: Abstract Display Example</title>

<meta name="description" content="GNU Emacs Lisp Reference Manual: Abstract Display Example">
<meta name="keywords" content="GNU Emacs Lisp Reference Manual: Abstract Display Example">
<meta name="resource-type" content="document">
<meta name="distribution" content="global">
<meta name="Generator" content="makeinfo">
<meta http-equiv="Content-Type" content="text/html; charset=utf-8">
<link href="index.html#Top" rel="start" title="Top">
<link href="Index.html#Index" rel="index" title="Index">
<link href="index.html#SEC_Contents" rel="contents" title="Table of Contents">
<link href="Abstract-Display.html#Abstract-Display" rel="up" title="Abstract Display">
<link href="Blinking.html#Blinking" rel="next" title="Blinking">
<link href="Abstract-Display-Functions.html#Abstract-Display-Functions" rel="prev" title="Abstract Display Functions">
<style type="text/css">
<!--
a.summary-letter {text-decoration: none}
blockquote.smallquotation {font-size: smaller}
div.display {margin-left: 3.2em}
div.example {margin-left: 3.2em}
div.indentedblock {margin-left: 3.2em}
div.lisp {margin-left: 3.2em}
div.smalldisplay {margin-left: 3.2em}
div.smallexample {margin-left: 3.2em}
div.smallindentedblock {margin-left: 3.2em; font-size: smaller}
div.smalllisp {margin-left: 3.2em}
kbd {font-style:oblique}
pre.display {font-family: inherit}
pre.format {font-family: inherit}
pre.menu-comment {font-family: serif}
pre.menu-preformatted {font-family: serif}
pre.smalldisplay {font-family: inherit; font-size: smaller}
pre.smallexample {font-size: smaller}
pre.smallformat {font-family: inherit; font-size: smaller}
pre.smalllisp {font-size: smaller}
span.nocodebreak {white-space:nowrap}
span.nolinebreak {white-space:nowrap}
span.roman {font-family:serif; font-weight:normal}
span.sansserif {font-family:sans-serif; font-weight:normal}
ul.no-bullet {list-style: none}
-->
</style>


<link rel="stylesheet" href="aquamacs.css" type="text/css" /></head>

<body lang="en" bgcolor="#FFFFFF" text="#000000" link="#0000FF" vlink="#800080" alink="#FF0000">
<a name="Abstract-Display-Example"></a>
<div class="header">
<p>
Previous: <a href="Abstract-Display-Functions.html#Abstract-Display-Functions" accesskey="p" rel="prev">Abstract Display Functions</a>, Up: <a href="Abstract-Display.html#Abstract-Display" accesskey="u" rel="up">Abstract Display</a> &nbsp; [<a href="index.html#SEC_Contents" title="Table of contents" rel="contents">Contents</a>][<a href="Index.html#Index" title="Index" rel="index">Index</a>]</p>
</div>
<hr>
<a name="Abstract-Display-Example-1"></a>
<h4 class="subsection">37.20.2 Abstract Display Example</h4>

<p>Here is a simple example using functions of the ewoc package to
implement a <em>color components</em> display, an area in a buffer that
represents a vector of three integers (itself representing a 24-bit RGB
value) in various ways.
</p>
<div class="example">
<pre class="example">(setq colorcomp-ewoc nil
      colorcomp-data nil
      colorcomp-mode-map nil
      colorcomp-labels [&quot;Red&quot; &quot;Green&quot; &quot;Blue&quot;])

(defun colorcomp-pp (data)
  (if data
      (let ((comp (aref colorcomp-data data)))
        (insert (aref colorcomp-labels data) &quot;\t: #x&quot;
                (format &quot;%02X&quot; comp) &quot; &quot;
                (make-string (ash comp -2) ?#) &quot;\n&quot;))
    (let ((cstr (format &quot;#%02X%02X%02X&quot;
                        (aref colorcomp-data 0)
                        (aref colorcomp-data 1)
                        (aref colorcomp-data 2)))
          (samp &quot; (sample text) &quot;))
      (insert &quot;Color\t: &quot;
              (propertize samp 'face
                          `(foreground-color . ,cstr))
              (propertize samp 'face
                          `(background-color . ,cstr))
              &quot;\n&quot;))))

(defun colorcomp (color)
  &quot;Allow fiddling with COLOR in a new buffer.
The buffer is in Color Components mode.&quot;
  (interactive &quot;sColor (name or #RGB or #RRGGBB): &quot;)
  (when (string= &quot;&quot; color)
    (setq color &quot;green&quot;))
  (unless (color-values color)
    (error &quot;No such color: %S&quot; color))
  (switch-to-buffer
   (generate-new-buffer (format &quot;originally: %s&quot; color)))
  (kill-all-local-variables)
  (setq major-mode 'colorcomp-mode
        mode-name &quot;Color Components&quot;)
  (use-local-map colorcomp-mode-map)
  (erase-buffer)
  (buffer-disable-undo)
  (let ((data (apply 'vector (mapcar (lambda (n) (ash n -8))
                                     (color-values color))))
        (ewoc (ewoc-create 'colorcomp-pp
                           &quot;\nColor Components\n\n&quot;
                           (substitute-command-keys
                            &quot;\n\\{colorcomp-mode-map}&quot;))))
    (set (make-local-variable 'colorcomp-data) data)
    (set (make-local-variable 'colorcomp-ewoc) ewoc)
    (ewoc-enter-last ewoc 0)
    (ewoc-enter-last ewoc 1)
    (ewoc-enter-last ewoc 2)
    (ewoc-enter-last ewoc nil)))
</pre></div>

<a name="index-controller-part_002c-model_002fview_002fcontroller"></a>
<p>This example can be extended to be a color selection widget (in
other words, the &ldquo;controller&rdquo; part of the &ldquo;model&ndash;view&ndash;controller&rdquo;
design paradigm) by defining commands to modify <code>colorcomp-data</code>
and to finish the selection process, and a keymap to tie it all
together conveniently.
</p>
<div class="smallexample">
<pre class="smallexample">(defun colorcomp-mod (index limit delta)
  (let ((cur (aref colorcomp-data index)))
    (unless (= limit cur)
      (aset colorcomp-data index (+ cur delta)))
    (ewoc-invalidate
     colorcomp-ewoc
     (ewoc-nth colorcomp-ewoc index)
     (ewoc-nth colorcomp-ewoc -1))))

(defun colorcomp-R-more () (interactive) (colorcomp-mod 0 255 1))
(defun colorcomp-G-more () (interactive) (colorcomp-mod 1 255 1))
(defun colorcomp-B-more () (interactive) (colorcomp-mod 2 255 1))
(defun colorcomp-R-less () (interactive) (colorcomp-mod 0 0 -1))
(defun colorcomp-G-less () (interactive) (colorcomp-mod 1 0 -1))
(defun colorcomp-B-less () (interactive) (colorcomp-mod 2 0 -1))

(defun colorcomp-copy-as-kill-and-exit ()
  &quot;Copy the color components into the kill ring and kill the buffer.
The string is formatted #RRGGBB (hash followed by six hex digits).&quot;
  (interactive)
  (kill-new (format &quot;#%02X%02X%02X&quot;
                    (aref colorcomp-data 0)
                    (aref colorcomp-data 1)
                    (aref colorcomp-data 2)))
  (kill-buffer nil))

(setq colorcomp-mode-map
      (let ((m (make-sparse-keymap)))
        (suppress-keymap m)
        (define-key m &quot;i&quot; 'colorcomp-R-less)
        (define-key m &quot;o&quot; 'colorcomp-R-more)
        (define-key m &quot;k&quot; 'colorcomp-G-less)
        (define-key m &quot;l&quot; 'colorcomp-G-more)
        (define-key m &quot;,&quot; 'colorcomp-B-less)
        (define-key m &quot;.&quot; 'colorcomp-B-more)
        (define-key m &quot; &quot; 'colorcomp-copy-as-kill-and-exit)
        m))
</pre></div>

<p>Note that we never modify the data in each node, which is fixed when the
ewoc is created to be either <code>nil</code> or an index into the vector
<code>colorcomp-data</code>, the actual color components.
</p>
<hr>
<div class="header">
<p>
Previous: <a href="Abstract-Display-Functions.html#Abstract-Display-Functions" accesskey="p" rel="prev">Abstract Display Functions</a>, Up: <a href="Abstract-Display.html#Abstract-Display" accesskey="u" rel="up">Abstract Display</a> &nbsp; [<a href="index.html#SEC_Contents" title="Table of contents" rel="contents">Contents</a>][<a href="Index.html#Index" title="Index" rel="index">Index</a>]</p>
</div>



</body>
</html>
